;   Include file for the USBSER PIC application.
;
;   This firmware is a USB to serial converter, using Embed's standard USB
;   driver on the host.
;
;   Peripheral usage:
;
;     Oscillator  -  Driven from 12 MHz external crystal.  With 4x PLL provides
;       the 48 MHz clock required for full speed USB operation.  The CPU runs
;       from this 48 MHz clock without being divided down.  The instruction rate
;       is therefore 12 MHz.
;
;     TMR2  -  Used as the main clock base in the INTR module, and sets the PWM
;       period as used in the SUPP module.  The timer configuration is
;       determined in this include file so that it is visible to all modules.
;       The timer is set up and managed in the INTR module.
;
;     UART  -  Provides the in-board interface to the USB enpoint 1 byte
;       streams.  Implements Embed USB device 10 protocol.  Managed in the UART
;       module.
;
;     USB  -  Managed in USB module.  Enumeration requests handled in USB0
;       module.
;
;     CCP1  -  Used in PWM mode to produce the pulses for driving the switching
;       power supply.  Only used when ISOLPWR set to TRUE.  Managed in the SUPP
;       module.
;
;     VREF  -  Supplies the reference to compare the power supply threshold
;       detect feedback signal to.  Only used when ISOLPWR is set to TRUE.
;       Managed in the SUPP module.
;
;     COMPARATOR 1  -  Compares the switching power supply feedback signal to
;       the VREF output.  This drives the PWM shutdown directly in hardware to
;       stop switching power supply pulses when the supply is above its
;       regulation threshold.  Only used when ISOLPWR is TRUE.  Managed in the
;       SUPP module.
;
/include "usbserlib.ins.aspic"

/const   debug_0flood bool = false ;sends max rate 0 bytes to host when TP1 low
/const   debug_txie bool = false ;show TXIE state
/const   debug_main bool = false ;show on out pin when in main loop
/const   debug_pins bool = true ;show final I/O pin assignments in INIT module
;
;   Application configuration parameters
;
/const   namemaxlen integer = 80 ;max allowed char in user-settable name string
/const   cmd_ustat integer = 255 ;USB status command opcode to send to client

gbankadr equ     0           ;put ordinary globals into access bank
;
;   Identify the taget hardware.  BOARD can be one of the following strings:
;
;     S5MAIN2  -  The USB to serial converter built into the second Sentry 5
;       prototype.
;
;     S5MAIN3  -  Jowa Sentry 5 board, version 3.
;
;   Derive low level configuration details from the target board type.  Features
;   can be enabled with the following preprocessor constants.
;
;     TXGO0, TXGO1, TXGO2  -  Bool constant.
;
;       Causes the INTn input be used for UART sending flow control.  When the
;       INTn input is high, UART data is transmitted when available.  When the
;       input is externally held low, then UART sending is inhibited.  Since the
;       USB is inherently flow controlled, this merely slows the data sent out
;       the UART without causing any loss.
;
;       These pins are passively pulled up.  The default is not to implement
;       any UART output inhibit, which allows the INTn pins to be used for other
;       purposes.
;
;     RXREADY output pin
;
;       If this pin is defined, it is driven high whenver the UART is ready to
;       receive another byte.
;
/const   board   string = "S5MAIN3"
/pick one by board

/option "S5MAIN2" "S5MAIN3"  ;Sentry 5 USB to CAN bridge
  /const usb_vid integer = 5824 ;USB vendor ID
  /const usb_pid integer = 1489 ;device ID within VID, Embed device 10
  /const usb_curr real = 0.050 ;USB power current draw, A
  /const usb_manuf string = "Jowa USA" ;manufacturer name
  /const usb_prod string = "Sentry 5" ;product name
  /const usb_name = "Jowa Sentry 5" ;Embed10 device name
  /const tick_per = 0.001    ;base clock tick period, seconds
  /const baud real = 273438  ;desired UART baud rate
  /const txgo1 bool = true   ;using TXGO on INT1 input
  /outbit rxready portb 2    ;pin to set high when ready to receive UART data

/optionelse                  ;BOARD didn't match a known board name
  /show "  Unsupported target board type " board " set in JDSP.INS.ASPIC"
         error   Board type
         end
  /stop
  /endpick
;
;   Make sure all the necessary constants are defined.
;
/if [not [exist "txgo0:const"]] then
  /const txgo0 bool = false
  /endif
/if [not [exist "txgo1:const"]] then
  /const txgo1 bool = false
  /endif
/if [not [exist "txgo2:const"]] then
  /const txgo2 bool = false
  /endif

/include "usbser_prepic.ins.aspic"
;
;   Define the set or registers that will be saved across calls to TASK_YIELD.
;
tsksave  set     0           ;build the mask of registers for TASK_YIELD to save
tsksave  set     tsksave | regf0
tsksave  set     tsksave | regf1
tsksave  set     tsksave | regf2
tsksave  set     tsksave | regf3
tsksave  set     tsksave | regf4
tsksave  set     tsksave | regf5
tsksave  set     tsksave | regf6
tsksave  set     tsksave | regf7

/include "(cog)src/pic/task_setup.ins.aspic"
/include "(cog)src/pic/usb_setup.ins.aspic"
/include "usbser_usb.ins.aspic" ;global definitions required by USB subsystem
;
;   Derived constants.
;
namemaxlen equ   [v namemaxlen]
         ;
         ;   Compute the timer 2 setup.  No code is generated, only the setup
         ;   parameters are derived from the desired clock tick period set by
         ;   TICK_PER, above.  The following preprocessor constants are set:
         ;
         ;     TMR2_PRE  -  timer 2 prescaler value: 1, 4, or 16
         ;
         ;     TMR2_PER  -  timer 2 period divide value: 1 - 256
         ;
         ;     TMR2_POS  -  timer 2 postscaler value: 1 - 16
         ;
         ;     TMR2_PERIOD  -  Actual timer 2 period in seconds.
         ;
         ;     TMR2_CYCLES  -  Actual timer 2 period in instruction cycles.
         ;
         ;     TMR2_PERPWM  -  Period for PWM (no postscaler) in seconds.
         ;
         ;     TMR2_CYPWM  -  Period for PWM in instruction cycles.
         ;
         ;     TMR2_FREQ  -  Actual timer 2 frequency, Hz.
         ;
         tmr2cy_closeper [v tick_per] ;compute timer setup

;**********
;
;   EEPROM allocation.  Each of the EE_xxx symbols is the offset of a particular
;   piece of non-volatile state from the beginning of the EEPROM.  Fixed
;   allocation of the EEPROM is performed here instead of letting the linker
;   place individual pieces of state so that consistancy is maintained between
;   firmware versions.  To the extent possible, newer firmware versions should
;   use the same EEPROM state in the same locations as previous versions.  This
;   at least makes it possible for non-volatile settings to be preserved for
;   some types of firmware updates, such as bootloads.  Note that this comment
;   does not guarantee any such mechanisms are actually implemented.
;
  cblock 0
         ee_namelen:1        ;number of characters in user-settable name string
         ee_name:namemaxlen  ;the user-settable name string characters
    endc

;**********
;
;   Global flag bits.
;
/flag    sin                 ;UART has a received character available
/flag    sout                ;UART can accept a new character to send
/flag    promlock            ;non-volatile memory is acquired by a task
/flag    uinbyte             ;a UART input byte is available in the FIFO
/flag    sendustat           ;send USTAT command to serial device

;*******************************************************************************
;
;   I/O pin declarations.
;

;***************************************
;
;   Define the I/O pins with fixed functions that can't be moved to other pins.
;
/if txgo0 then
  /inbit txgo portb 0 pup    ;allows UART sending, using INT0
  /endif
/if txgo1 then
  /inbit txgo portb 1 pup    ;allows UART sending, using INT1
  /endif
/if txgo2 then
  /inbit txgo portb 2 pup    ;allows UART sending, using INT2
  /endif

/outbit  pgc     portb 6     ;programming clock line
/outbit  pgd     portb 7     ;programming data line

/outbit  tx      portc 6 n   ;UART transmit line
/inbit   rx      portc 7     ;UART receive line

////////////////////////////////////////
//
//   Define all remaining I/O pins.  Unused pins are named with the pin name
//   (like RA1, RB5) and set to outputs driven low.
//
//   Pin function assignments that can vary per implementation are defined by
//   constants named
//
//     name_IN_PIN
//     name_OUT_PIN
//
//   in the board configuration section earlier in this file.  These constants
//   are the pin names ("RA1", "RB5", etc) to use for that function.  Such pins
//   will be defined /INBIT or /OUTBIT, named NAME, and set to the indicated
//   pin.  It is an error if multiple functions are specified for the same pin,
//   or a pin that has a fixed function assigned (above).  An error will also be
//   flagged when a xxx_IN_PIN or xxx_OUT_PIN constant exists to an unavailable
//   or non-existant pin.
//
/call AssignPin A 0
/call AssignPin A 1
/call AssignPin A 2
/call AssignPin A 3
/call AssignPin A 4
/call AssignPin A 5

/call AssignPin B 0
/call AssignPin B 1
/call AssignPin B 2
/call AssignPin B 3
/call AssignPin B 4
/call AssignPin B 5
/call AssignPin B 6
/call AssignPin B 7

/call AssignPin C 0
/call AssignPin C 1
/call AssignPin C 2
/call AssignPin C 6
/call AssignPin C 7

/call check_pin_err //check for pin assignment errors
